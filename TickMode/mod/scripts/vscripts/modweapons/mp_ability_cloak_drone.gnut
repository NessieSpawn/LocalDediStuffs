global function MpAbilityCloakDrone_Init

global function OnWeaponPrimaryAttack_ability_cloak_drone
global function OnProjectileCollision_ability_cloak_drone

const float CLOAK_DRONE_LIFETIME = 20
// better check after changing base_gametype
const float CLOAK_DRONE_NO_REGEN_TIME = 20

void function MpAbilityCloakDrone_Init()
{
    
}

var function OnWeaponPrimaryAttack_ability_cloak_drone( entity weapon, WeaponPrimaryAttackParams attackParams )
{
    entity ownerPlayer = weapon.GetWeaponOwner()

    entity deployable = ThrowDeployable( weapon, attackParams, DEPLOYABLE_THROW_POWER, OnCloakDroneReleased )
    #if SERVER
        SendHudMessage( ownerPlayer, "扔出隐身无人机", -1, -0.35, 255, 255, 100, 255, 0, 3, 0 )
        thread ModdedCloakCooldownThink( weapon, CLOAK_DRONE_NO_REGEN_TIME )
    #endif

    if ( deployable )
    {
        entity player = weapon.GetWeaponOwner()

        #if SERVER
            string projectileSound = GetGrenadeProjectileSound( weapon )
            if ( projectileSound != "" )
                EmitSoundOnEntity( deployable, projectileSound )

            weapon.w.lastProjectileFired = deployable
        #endif
    }

    PlayerUsedOffhand( ownerPlayer, weapon )

    return weapon.GetWeaponSettingInt( eWeaponVar.ammo_min_to_fire )
}

void function OnProjectileCollision_ability_cloak_drone( entity projectile, vector pos, vector normal, entity hitEnt, int hitbox, bool isCritical )
{
    #if SERVER
		entity owner = projectile.GetOwner()
		if ( !IsAlive( owner ) ) // only deploy cloak field if owner still alive
		{
			if ( IsValid( projectile ) )
			{
				projectile.Destroy()
				return
			}
		}
	#endif

    return OnProjectileCollision_weapon_deployable( projectile, pos, normal, hitEnt, hitbox, isCritical )
}

void function OnCloakDroneReleased( entity projectile )
{
	#if SERVER
		entity drone = SpawnCloakDrone( projectile.GetTeam(), projectile.GetOrigin(), < 0,0,0 >, < 0,0,0 >, projectile.GetOwner() )
		thread AfterTimeDestroyDrone( drone, projectile.GetOwner(), CLOAK_DRONE_LIFETIME )
		projectile.GrenadeExplode( < 0,0,20 > )
	#endif
}

#if SERVER
void function AfterTimeDestroyDrone( entity drone, entity owner, float delay )
{
	owner.EndSignal( "OnDeath" )
	owner.EndSignal( "OnDestroy" )

	OnThreadEnd(
		function() : ( drone )
		{
			if( IsValid( drone ) )
				drone.SetHealth( 0 )
		}
	)
	
	wait delay
}

void function ModdedCloakCooldownThink( entity weapon, float cooldown )
{
	weapon.SetWeaponPrimaryClipCountAbsolute( 0 )
	weapon.AddMod( "no_regen" )
	entity weaponOwner = weapon.GetWeaponOwner()
	weaponOwner.EndSignal( "OnDeath" )
	weaponOwner.EndSignal( "OnDestroy" )

	int offhandSlot = 0
	for ( int i = 0; i <= OFFHAND_MELEE; i++ ) // OFFHAND_MELEE is the largest
	{
		entity nowWeapon = weaponOwner.GetOffhandWeapon( i )
		if( IsValid( nowWeapon ))
		{
			if( nowWeapon.GetWeaponClassName() == "mp_ability_cloak" )
				offhandSlot = i
		}
	}

	OnThreadEnd(
		function(): ( weapon, weaponOwner, offhandSlot )
		{
			if( IsValid( weapon ) )
				weapon.RemoveMod( "no_regen" )
			else if( IsValid( weaponOwner ) ) // player has interrupted their cloak and get a new one
			{
				entity cloakWeapon = weaponOwner.GetOffhandWeapon( offhandSlot )
				if( IsValid( cloakWeapon ) )
					cloakWeapon.RemoveMod( "no_regen" )
			}
		}
	)

	wait cooldown
}
#endif